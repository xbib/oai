plugins {
    id "com.github.spotbugs" version "2.0.0"
    id "org.sonarqube" version "2.6.1"
    id "io.codearte.nexus-staging" version "0.11.0"
    id "org.xbib.gradle.plugin.asciidoctor" version "1.6.0.1"
}

apply plugin: "io.codearte.nexus-staging"

subprojects {

    apply plugin: 'java-library'
    apply plugin: 'maven'
    apply plugin: 'pmd'
    apply plugin: 'checkstyle'
    apply plugin: "com.github.spotbugs"
    apply plugin: 'org.xbib.gradle.plugin.asciidoctor'

    repositories {
        mavenCentral()
    }

    dependencies {
        testCompile "org.junit.jupiter:junit-jupiter-api:${project.property('junit.version')}"
        testCompile "org.junit.jupiter:junit-jupiter-params:${project.property('junit.version')}"
        testCompile "org.junit.jupiter:junit-jupiter-engine:${project.property('junit.version')}"
        //testCompile "org.junit.vintage:junit-vintage-engine:${project.property('junit.version')}"
        testCompile "org.xbib:bibliographic-character-sets:${project.property('xbib-bibliographic-character-sets.version')}"
    }

    compileJava {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    compileTestJava {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11

    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:all"
    }

    clean {
        delete 'out'
    }

    test {
        useJUnitPlatform()
        failFast = false
        testLogging {
            events 'STARTED', 'PASSED', 'FAILED', 'SKIPPED'
            showStandardStreams = false
        }
        afterSuite { desc, result ->
            if (!desc.parent) {
                println "\nTest result: ${result.resultType}"
                println "Test summary: ${result.testCount} tests, " +
                        "${result.successfulTestCount} succeeded, " +
                        "${result.failedTestCount} failed, " +
                        "${result.skippedTestCount} skipped"
            }
        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier 'javadoc'
    }

    artifacts {
        archives sourcesJar, javadocJar
    }

    ext {
        user = 'xbib'
        projectName = 'oai'
        projectDescription = 'Open Archive Initiative library for Java'
        scmUrl = 'https://github.com/xbib/oai'
        scmConnection = 'scm:git:git://github.com/xbib/oai.git'
        scmDeveloperConnection = 'scm:git:git://github.com/xbib/oai.git'
    }

    task sonatypeUpload(type: Upload, dependsOn: build) {
        group = 'publish'
        configuration = configurations.archives
        uploadDescriptor = true
        repositories {
            if (project.hasProperty('ossrhUsername')) {
                mavenDeployer {
                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                    repository(url: uri(ossrhReleaseUrl)) {
                        authentication(userName: ossrhUsername, password: ossrhPassword)
                    }
                    snapshotRepository(url: uri(ossrhSnapshotUrl)) {
                        authentication(userName: ossrhUsername, password: ossrhPassword)
                    }
                    pom.project {
                        groupId project.group
                        artifactId project.name
                        version project.version
                        name project.name
                        description projectDescription
                        packaging 'jar'
                        inceptionYear '2016'
                        url scmUrl
                        organization {
                            name 'xbib'
                            url 'http://xbib.org'
                        }
                        developers {
                            developer {
                                id user
                                name 'JÃ¶rg Prante'
                                email 'joergprante@gmail.com'
                                url 'https://github.com/jprante'
                            }
                        }
                        scm {
                            url scmUrl
                            connection scmConnection
                            developerConnection scmDeveloperConnection
                        }
                        licenses {
                            license {
                                name 'The Apache License, Version 2.0'
                                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            }
                        }
                    }
                }
            }
        }
    }

    nexusStaging {
        packageGroup = "org.xbib"
    }

    spotbugs {
        effort = "max"
        reportLevel = "low"
    }

    tasks.withType(com.github.spotbugs.SpotBugsTask) {
        ignoreFailures = true
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    pmd {
        toolVersion = '6.11.0'
        ruleSets = ['category/java/bestpractices.xml']
        ignoreFailures = true
    }

    checkstyle {
        configFile = rootProject.file('config/checkstyle/checkstyle.xml')
        ignoreFailures = true
        showViolations = true
    }

    sonarqube {
        properties {
            property "sonar.projectName", "${project.group} ${project.name}"
            property "sonar.sourceEncoding", "UTF-8"
            property "sonar.tests", "src/test/java"
            property "sonar.scm.provider", "git"
            property "sonar.junit.reportsPath", "build/test-results/test/"
        }
    }
}
